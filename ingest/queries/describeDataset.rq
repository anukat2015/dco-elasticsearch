PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>

DESCRIBE ?dataset ?dcoid ?mostSpecificType ?label ?publicationYear ?community ?group ?project ?authorship ?author ?researchArea ?authorOrganization ?dataType ?citation ?distribution ?file ?fileURL
WHERE {
  ?dataset dco:hasDcoId ?dcoid .
  ?dataset vitro:mostSpecificType ?mostSpecificType .
  ?dataset rdfs:label ?label .
  OPTIONAL { ?dataset dco:yearOfPublication ?publicationYear . }
  OPTIONAL { ?dataset dco:associatedDCOCommunity ?community }
  OPTIONAL { ?dataset dco:associatedDCOPortalGroup ?group }
  OPTIONAL { ?dataset dco:isDatasetOf ?project }
  OPTIONAL {
    ?dataset vivo:relatedBy ?authorship .
    ?authorship vivo:relates ?author .
    ?author a foaf:Person .
    OPTIONAL { ?author vivo:hasResearchArea ?researchArea }
    OPTIONAL { ?author dco:inOrganization ?authorOrganization }
  }
  OPTIONAL {
        ?dataset dco:hasDistribution ?distribution .
        ?distribution dco:hasFile ?file .
        ?distribution rdfs:label ?distributionLabel .
        ?distribution dco:accessURL ?distributionURL .
        ?file rdfs:label ?fileLabel .
        ?file dco:downloadURL ?fileURL .
  }
  OPTIONAL { ?dataset dco:hasDataType ?dataType }
  OPTIONAL { ?dataset bibo:cites ?citation }
}